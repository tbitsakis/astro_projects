pro art_stars_xy
; NAME:
;		arti_stars_xy
;
; PURPOSE:
;		It reads an image and places artficial stars of M mag in X, Y random positions.
;
; CALLING SEQUENCE:
;		art_stars_xy
;
; INPUT:
;		A catalog containing the paths to the images, number of inserted artificial stars per image, 
;		number of repeats per image, magnitude limits, and margin to be left without artificial stars.
;
; RESULT:
;		A catalog containing the new images+artificial stars to be generated by DAOPHOT/addstar.
;
; HISTORY:
;       Written, 2017, Theo Bitsakis, IRyA-UNAM.
;-

;=================== Edit only here =======================
; Edit and change only the following parameters
	catalog="MCPS_wcs/Tiles.txt" ; Input image catalog
	N_stars=5 ; Number of stars to input per image 
	N_image=10000 ; Number of images to generate each time
	mag_up=13 ; Upper magnitude to test completeness
	mag_low=23 ; Lower magnitude to test completenes
	marg_size=2 ; Avoid margins (in px)
	col_up=2.0  ; Upper color limits
	col_low=-1.2  ; Lower color limits
;==========================================================

	nm1=""
	readcol,catalog,nm,ra,dec,rad,dens,tile,xb1,xb2,yb1,yb2,xu1,yu1,xv1,yv1,xi1,yi1,format='a,f,f,f,f,a,i,i,i,i,i,i,i,i,i,i'

	n_images=n_elements(nm)-1 ; Number of images in catalog

	nm1='cluster_images/'+nm+'_b.fits'

	ans=''
	read,ans,prompt='Which color you which to reproduce? (UV, BV, VI) '
	if ans eq 'UV' or ans eq 'uv' then out_path='artificial_stars_UV.dat'
	if ans eq 'BV' or ans eq 'bv' then out_path='artificial_stars_BV.dat'
	if ans eq 'VI' or ans eq 'vi' then out_path='artificial_stars_VI.dat'

	openw,10,out_path
	printf,10,'#Image       No      x      y       col      mag1      mag2'

	for i1=0,n_images do begin

		im=mrdfits(nm1[i1],0,h)

		sz=size(im)

		Dx=sz[1]-marg_size & Dy=sz[2]-marg_size

		for i2=0,N_image do begin
			pos_x=fix(marg_size+Dx*randomu(seed,N_stars))
			pos_y=fix(marg_size+Dy*randomu(seed,N_stars))
			col=col_low+(col_up-col_low)*randomu(seed,N_stars)
			mag2=mag_up+(mag_low-mag_up)*randomu(seed,N_stars)
			mag1=col+mag2

			for j=0,N_stars-1 do printf,10,nm[i1],i2,pos_x[j],pos_y[j],col[j],mag1[j],mag2[j],format='(a,i,i,i,5x,f5.1,5x,f5.1,5x,f5.1)'

		endfor

	endfor


close,10


end